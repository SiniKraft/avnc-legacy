plugins {
    id 'com.android.application' version '8.2.2'
    id 'org.jetbrains.kotlin.android' version '1.9.24'
    id 'org.jetbrains.kotlin.kapt' version '1.9.24'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.24'
    id "org.jetbrains.kotlin.plugin.parcelize" version "1.9.24"
}

android {
    namespace 'com.gaurav.avnc'

    compileSdk 34
    ndkVersion "23.2.8568313"

    defaultConfig {
        applicationId "com.gaurav.avnc"
        minSdk 17
        targetSdk 34
        versionCode 39
        versionName "2.6.3"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/roomSchema/".toString()]
            }
        }

        externalNativeBuild {
            cmake {
                //We are not using STL
                arguments '-DANDROID_STL=none'
                targets 'native-vnc', 'vncclient', 'turbojpeg-static', 'wolfssl'
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/roomSchema".toString())
    }

    buildTypes {

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' (debug)'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        ci {
            initWith(release)
            applicationIdSuffix '.ci'
            versionNameSuffix ' (CI)'
        }
    }

    buildFeatures {
        dataBinding true
        buildConfig true
    }

    externalNativeBuild {
        cmake {
            version '3.22.1'
            path file('CMakeLists.txt')
        }
    }

    bundle {
        density {
            enableSplit false
        }
        language {
            enableSplit false
        }
    }
}

kotlin {
    jvmToolchain(17)
}

dependencies {
    implementation "androidx.core:core-ktx:1.12.0"
    implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation "androidx.fragment:fragment-ktx:1.6.2"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0-alpha03"
    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha05"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.2"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.drawerlayout:drawerlayout:1.1.1"

    def roomVersion = "2.6.1"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    implementation "com.google.android.material:material:1.7.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0'
    implementation 'org.connectbot:sshlib:2.2.23'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test:runner:1.6.0-alpha04"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.6.0-alpha01"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.6.0-alpha01"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.6.0-alpha01"
    androidTestImplementation "androidx.test:rules:1.6.0-alpha01"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.2.0-alpha01"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"
    androidTestImplementation "io.mockk:mockk-android:1.12.0"
}
